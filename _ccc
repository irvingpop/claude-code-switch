#compdef ccc

_ccc_accounts() {
  local accounts
  if [[ -f ~/.ccm_accounts ]]; then
    accounts=(${(f)"$(cut -d'|' -f1 ~/.ccm_accounts 2>/dev/null)"})
    _describe 'account' accounts
  fi
}

_ccc() {
  local curcontext="$curcontext" state line
  typeset -A opt_args

  _arguments -C \
    '1: :->model_or_account' \
    '*::arg:->args'

  case $state in
    model_or_account)
      local -a models accounts combined
      models=(
        'sonnet:Claude Sonnet 4.5'
        's:Claude Sonnet 4.5 (alias)'
        'opus:Claude Opus 4.5'
        'o:Claude Opus 4.5 (alias)'
        'haiku:Claude Haiku 4.5'
        'h:Claude Haiku 4.5 (alias)'
      )

      if [[ -f ~/.ccm_accounts ]]; then
        accounts=(${(f)"$(cut -d'|' -f1 ~/.ccm_accounts 2>/dev/null)"})
        combined=($models)
        for acc in $accounts; do
          combined+=("$acc:Switch to account '$acc'")
        done
        _describe 'model or account' combined
      else
        _describe 'model' models
      fi
      ;;
    args)
      _arguments '*: :_files'
      ;;
  esac
}

_ccc "$@"
